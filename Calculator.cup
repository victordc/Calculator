package Calculator;

import java_cup.runtime.*;

parser code {:
	public static void main(String args[]) throws Exception {
		new parser(new Yylex(System.in)).parse();
	}
:}

terminal SEMI, PLUS, MINUS, TIMES, MOD, DIVIDE, LPAREN, RPAREN;
terminal Integer NUMBER;

non terminal expr_list, expr_part;
non terminal Integer expr;

precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE, MOD;

expr_list ::= expr_list expr_part | expr_part;
expr_part ::= expr:e {: System.out.println(" = "+e+";"); :} SEMI;
expr      ::= NUMBER:n
	      {: RESULT=n; :}
            | expr:l PLUS expr:r
	      {: RESULT=new Integer(l.intValue() + r.intValue()); :}
            | expr:l MINUS expr:r
	      {: RESULT=new Integer(l.intValue() - r.intValue()); :}
	    | expr:l TIMES expr:r
	      {: RESULT=new Integer(l.intValue() * r.intValue()); :}
	    | expr:l DIVIDE expr:r
	      {: RESULT=new Integer(l.intValue() / r.intValue()); :}
	    | expr:l MOD expr:r
	      {: RESULT=new Integer(l.intValue() % r.intValue()); :}
	    | LPAREN expr:e RPAREN
	      {: RESULT=e; :}
	    ;
